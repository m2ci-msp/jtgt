plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

version '0.8.0-SNAPSHOT'
group "io.github.m2ci-msp"
description "Java TextGridTools"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.json', name: 'json', version: '20220924'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
}

/**********************************************************************************************
 ** Formatting part
 **********************************************************************************************/

task autoFormatCode {
    def config = file("astyle.conf")
    inputs.files config
    outputs.upToDateWhen { false }
    doLast {
        fileTree(dir: '.', include: '**/*.java').each { java_file ->
            exec {
                commandLine "astyle", "--options=$config", java_file
            }
        }
    }
}

/**********************************************************************************************
 ** Project meta + publishing part
 **********************************************************************************************/

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/m2ci-msp/jtgt'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        id = 'seblemaguer'
                        name = 'SÃ©bastien Le Maguer'
                    }
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/m2ci-msp/jtgt.git'
                    developerConnection = 'scm:git:ssh://git@github.com:m2ci-msp/jtgt.git'
                    url = 'https://github.com/m2ci-msp/jtgt'
                }
            }
        }
    }
    repositories {
        maven {
            name 'NewOSSRH'
            url isReleaseVersion ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
                    : 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
