plugins {
    id 'java-library'
    id 'maven-publish'
}

version '0.6.1'
group "org.m2ci.msp"
description "Java TextGridTools"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.json', name: 'json', version: '20180130'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
}

/**********************************************************************************************
 ** Formatting part
 **********************************************************************************************/

task autoFormatCode {
    def config = file("astyle.conf")
    inputs.files config
    outputs.upToDateWhen { false }
    doLast {
        fileTree(dir: '.', include: '**/*.java').each { java_file ->
            exec {
                commandLine "astyle", "--options=$config", java_file
            }
        }
    }
}

/**********************************************************************************************
 ** Project meta + publishing part
 **********************************************************************************************/

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/m2ci-msp/maven/jtgt'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
